#!/usr/bin/perl

=head1 NAME

sked - iCal to Remind Converter

=cut

use 5.010;

use strict;
use warnings;

use feature 'switch';
use filetest 'access';

# /usr/include/sysexits.h
# - would be nice to use sysexits.ph instead one day
#BEGIN { require "sysexits.ph"; }
use constant EX_OK		=> 0;	# successful termination
use constant EX_USAGE		=> 64;	# command line usage error
use constant EX_DATAERR		=> 65;	# data format error
use constant EX_NOINPUT		=> 66;	# cannot open input
use constant EX_NOUSER		=> 67;	# addressee unknown
use constant EX_NOHOST		=> 68;	# host name unknown
use constant EX_UNAVAILABLE	=> 69;	# service unavailable
use constant EX_SOFTWARE	=> 70;	# internal software error
use constant EX_OSERR		=> 71;	# system error (e.g., can't fork)
use constant EX_OSFILE		=> 72;	# critical OS file missing
use constant EX_CANTCREAT	=> 73;	# can't create (user) output file
use constant EX_IOERR		=> 74;	# input/output error
use constant EX_TEMPFAIL	=> 75;	# temp failure; user is invited to retry
use constant EX_PROTOCOL	=> 76;	# remote error in protocol
use constant EX_NOPERM		=> 77;	# permission denied
use constant EX_CONFIG		=> 78;	# configuration error

use Getopt::Long;
use Pod::Usage;
use Data::ICal 0.16;
use Date::ICal 1.72;
use DateTime 0.61;
use DateTime::Event::ICal 0.10;
use DateTime::Format::ICal 0.09;
use POSIX;

use Data::Dumper;

=head1 SYNOPSIS

sked [options]

=head1 OPTIONS

=cut

=over

=item B<--help, -h>

Print a brief help message and exits.

=item B<--man>

Prints the manual page and exits.

=item B<--debug, -d>

Enable debugging.

=item B<--input, -i>

Input filename.

=item B<--output, -o>

Output directory.

=item B<--prefix, -p>

Summary prefix (default: '{m}').

=back

=head1 DESCRIPTION

B<This program> is all teh awesomes.

=head1 DEPENDENCIES

=over

=item Data::ICal 0.16 L<http://search.cpan.org/~tsibley/Data-ICal/>

=back

=cut

my ($help, $man);
my ($debug);
my ($input, $output);
my $prefix = '{m}';

GetOptions(
	'help|h'	=> \$help,
	'man'		=> \$man,

	'debug|d'	=> \$debug,

	'input|i=s'	=> \$input,
	'output|o=s'	=> \$output,

	'prefix|p=s'	=> \$prefix,
) or pod2usage(-exitstatus => EX_USAGE, -verbose => 2);

pod2usage(-exitstatus => EX_OK, -verbose => 1)
	if ($help);
pod2usage(-exitstatus => EX_OK, -verbose => 2)
	if ($man);

unless (defined($input)) {
	print STDERR "'input' file is required\n";
	exit EX_USAGE;
}
unless (defined($output) && -d "$output" 
		&& -r "$output" && -w "$output" && -x "$output") {
	print STDERR "'output' directory does not exist or it not accessible\n";
	exit EX_USAGE;
}
$prefix .= ' ';

my $cal;
if ($input eq '-') {
	my $data;
	$data .= $_
		while (<STDIN>);
	close STDIN;

	$cal = Data::ICal->new(data => $data);
} else {
	$cal = Data::ICal->new(filename => $input);
}
unless ($cal) {
	print STDERR $cal->error_message;
	exit EX_DATAERR;
}

our %globals = (
	prodid	=> $cal->property('prodid')->[0]->value,
	method	=> $cal->property('method')->[0]->value,
);

# HACK
our @sigblocks = (
	"\n-- \n.*\$",
	"\n\n[^\n]+ @ Telemetry\n.*\$",
);

DateTime->DefaultLocale(POSIX::setlocale(LC_CTYPE));

my @events;
my %tzids;
foreach my $entry (sort { # make sure Data::ICal::Entry::TimeZone is first
			return  0
				if (ref($a) eq ref($b));
			return -1
				if (ref($a) eq 'Data::ICal::Entry::TimeZone');
			return  1
				if (ref($b) eq 'Data::ICal::Entry::TimeZone');
			return  0;
		} @{$cal->entries}) {
	given(ref $entry) {
		when ('Data::ICal::Entry::Event') {
			push @events, event($entry);
		}
		when ('Data::ICal::Entry::TimeZone') {
			my ($name, $tzid) = tzid($entry);
			$tzids{$name} = $tzid
				if (scalar @$tzid);
		}
		default {
			DEBUG("ignoring unknown entry type $_");
		}
	}
}

# cannot do anything if there are no actual (parsable) events
unless (scalar @events > 0) {
	print STDERR "there has to be at least one Event entry\n";
	exit EX_DATAERR;
}

print STDERR Dumper \@events;
print STDERR Dumper \%tzids;

foreach my $event (@events) {
	unless (defined($event->{_summary})
			&& defined($event->{_dtstart})
			&& defined($event->{_dtend}) ) {
		print STDERR "missing fields\n";
		exit EX_DATAERR;
	}

	my $dtstart = DateTime::Format::ICal->parse_datetime(
			$event->{_dtstart}->ical
	);
	#$dtstart->set_time_zone('local');

	my $output = $dtstart->strftime('REM %e %b %Y AT %H:%M');

	if (defined($event->{alarm})) {
		my $aevent = $event->{alarm}->properties;
		unless (defined($aevent->{trigger})
				&& defined($aevent->{action}) ) {
			print STDERR "missing fields\n";
			exit EX_DATAERR;
		}

		my $d = Date::ICal::Duration->new(
				ical => $aevent->{trigger}->[0]->value);
		$output .= ' +' . $d->minutes;
	}

	if (defined($event->{_dtend})) {
		my $dtend = DateTime::Format::ICal->parse_datetime(
				$event->{_dtend}->ical
		);
		#$dtend->set_time_zone('local');

		my $dur		= $dtend - $dtstart;
		my $dhours	= (defined($dur->hours)) ? $dur->hours : 0;
		my $dmins	= (defined($dur->minutes)) ? $dur->minutes : 0;

		$output .= " DURATION $dhours:" . sprintf('%0.2d', $dmins);
	}

	my $summary = $prefix . $event->{_summary};
	if (defined($event->{_description})) {
		my $description	= $event->{_description};
		  $description =~ s/\n/%_\\\n/g;

		$output .= " MSF %\"$summary%\"%_\\\n$description";
	} else {
		$output .= " MSG $summary"
	}

	print "$output\n";
}

exit EX_OK;

sub DEBUG {
	my $msg = shift;

	print STDERR "DEBUG: $msg\n"
		if (defined($debug));
}

sub event {
	my $entry = shift;

	my $event = $entry->properties;
	unless (defined($event->{uid})) {
		print STDERR "missing UID, bailing\n";
		exit EX_DATAERR;
	}

	# note we occasionally use 'continue' for further fixups
	foreach (keys %$event) {
		when(/uid|summary|description|dtstart|dtend/) {
			$event->{"_$_"} = $event->{$_}->[0]->value;
			continue;
		}
		when(/description/) {
			$event->{_description} =~ s/$_//ms
				foreach (@sigblocks);
			continue;
		}
		when(/summary|description/) {
			# prune non-ASCII
			$event->{"_$_"} =~ s/[^[:print:]\n]//g;
			# prune whitespace
			$event->{"_$_"} =~ s/^\s*//s;
			$event->{"_$_"} =~ s/\s*$//s;
			# flatten newlines
			$event->{"_$_"} =~ s/\n+/\n/g;
		}
		when(/dtstart|dtend/) {
			$event->{"_$_"} = Date::ICal->new(ical => $event->{"_$_"});
		}
		when(/organizer/) {
			$event->{"_$_"} = $event->{$_}->[0];
		}
	}

	# some people lead the description with the summary
	$event->{_description} =~ s/^\Q$event->{_summary}\E\n//ms
		if (defined($event->{_summary}) && defined($event->{_description}));

	foreach my $subentry (@{$entry->entries}) {
		given(ref $subentry) {
			when ('Data::ICal::Entry::Alarm::Display') {
				$event->{alarm} = $subentry;
			}
			default {
				DEBUG("ignoring unknown subentry type $_");
			}
		}
	}

	given($globals{'prodid'}) {
		when ('Microsoft Exchange Server 2010') {
			$event = fixup_event_msexchange($event)
		}
	}

	delete $event->{_description}
		if ($event->{_description} eq '');

	return $event;
}

sub tzid {
	my $entry = shift;

	unless ($entry->property('tzid')) {
		print STDERR "missing TZID, bailing\n";
		exit EX_DATAERR;
	}
	my $name = $entry->property('tzid')->[0]->value;

	my @tzid;
	foreach my $subentry (@{$entry->entries}) {
		my $props = $subentry->properties;
		unless (defined($props->{dtstart})
				&& defined($props->{rrule})
				&& defined($props->{tzoffsetfrom})
				&& defined($props->{tzoffsetto})) {
			DEBUG("missing needed TIMEZONE properties, skipping\n");
			next;
		}

		# DTSTART:16010101T020000
		my $dtstart = DateTime::Format::ICal->parse_datetime(
				$props->{dtstart}->[0]->value
		);
		# FREQ=YEARLY;INTERVAL=1;BYDAY=-1SU;BYMONTH=10
		my %rule = map { split /=/ }
				split /;/, $props->{rrule}->[0]->value;
		my $set = DateTime::Event::ICal->recur(
			dtstart		=> $dtstart,
			freq		=> lc $rule{FREQ},
			interval	=> $rule{INTERVAL},
			bymonth		=> $rule{BYMONTH},
			byday		=> lc $rule{BYDAY},
		);

		push @tzid, {
			from	=> $props->{tzoffsetfrom}->[0]->value,
			to	=> $props->{tzoffsetto}->[0]->value,
			set	=> $set,
		}
	}

	return ($name, \@tzid);
}

sub fixup_event_msexchange {
	my $entry = shift;

	# remove big pointless prepended drivel
	$entry->{_description} =~ s/^.*?(?:\*~){9}\*[^[:print:]]*//s;

	return $entry;
}

exit EX_OK;

=head1 AUTHOR

Alexander Clouter E<lt>L<alex@digriz.org.uk>E<gt>

=head1 LICENSE AND COPYRIGHT

Copyright (C) 2013  Alexander Clouter E<lt>L<alex@digriz.org.uk>E<gt>

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but B<without any warranty>; without even the implied warranty of
B<merchantability> or B<fitness for a particular purpose>.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see L<http://www.gnu.org/licenses/>.

=cut
